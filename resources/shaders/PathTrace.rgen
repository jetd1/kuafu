#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_ARB_shader_clock : enable

#include "base/Camera.glsl"
#include "base/PushConstants.glsl"
#include "base/Ray.glsl"
#include "base/Sampling.glsl"

layout( location = 0 ) rayPayloadEXT RayPayLoad ray;
layout( location = 1 ) rayPayloadEXT bool isShadowed;

layout( binding = 0, set = 0 ) uniform accelerationStructureEXT topLevelAS;
layout( binding = 1, set = 0, rgba32f ) uniform image2D image;

//#define VARIANCE
//#define HEATMAP

#ifdef VARIANCE
layout( binding = 2, set = 0 ) buffer VarianceBuffer
{
  float v[];
}
varianceBuffer;
#endif

#ifdef HEATMAP
// From RayTracingInVulkan by GSnoopy https://github.com/GPSnoopy/RayTracingInVulkan
vec3 heatmap( float t )
{
  const vec3 c[10] = {
    vec3( 0.0f / 255.0f, 2.0f / 255.0f, 91.0f / 255.0f ),
    vec3( 0.0f / 255.0f, 108.0f / 255.0f, 251.0f / 255.0f ),
    vec3( 0.0f / 255.0f, 221.0f / 255.0f, 221.0f / 255.0f ),
    vec3( 51.0f / 255.0f, 221.0f / 255.0f, 0.0f / 255.0f ),
    vec3( 255.0f / 255.0f, 252.0f / 255.0f, 0.0f / 255.0f ),
    vec3( 255.0f / 255.0f, 180.0f / 255.0f, 0.0f / 255.0f ),
    vec3( 255.0f / 255.0f, 104.0f / 255.0f, 0.0f / 255.0f ),
    vec3( 226.0f / 255.0f, 22.0f / 255.0f, 0.0f / 255.0f ),
    vec3( 191.0f / 255.0f, 0.0f / 255.0f, 83.0f / 255.0f ),
    vec3( 145.0f / 255.0f, 0.0f / 255.0f, 65.0f / 255.0f )
  };

  const float s = t * 10.0f;

  const int cur = int( s ) <= 9 ? int( s ) : 9;
  const int prv = cur >= 1 ? cur - 1 : 0;
  const int nxt = cur < 9 ? cur + 1 : 9;

  const float blur = 0.8f;

  const float wc = smoothstep( float( cur ) - blur, float( cur ) + blur, s ) * ( 1.0 - smoothstep( float( cur + 1 ) - blur, float( cur + 1 ) + blur, s ) );
  const float wp = 1.0 - smoothstep( float( cur ) - blur, float( cur ) + blur, s );
  const float wn = smoothstep( float( cur + 1 ) - blur, float( cur + 1 ) + blur, s );

  const vec3 r = wc * c[cur] + wp * c[prv] + wn * c[nxt];
  return vec3( clamp( r.x, 0.0, 1.0 ), clamp( r.y, 0.0, 1.0 ), clamp( r.z, 0.0, 1.0 ) );
}
#endif

void main( )
{
  // maps an entry in the 2D array (image grid) to a 1D array
  uint mapping = gl_LaunchIDEXT.y * gl_LaunchSizeEXT.x + gl_LaunchIDEXT.x;
  uint seed    = tea( mapping, int( clockARB( ) ) );
  vec3 colors  = vec3( 0.0 );

#ifdef VARIANCE
  float meanX = 0.0;
  float meanY = 0.0;
  float meanZ = 0.0;
  float valuesX[100];
  float valuesY[100];
  float valuesZ[100];
#endif

  uint timeStart = uint( clockARB( ) );

  for ( uint i = 0; i < sampleRatePerPixel; ++i )
  {
    ray.seed = tea( gl_LaunchIDEXT.y * gl_LaunchSizeEXT.x + gl_LaunchIDEXT.x, int( clockARB( ) ) );

    // Jitter position within pixel to get free AA.
    vec2 positionWithinPixel   = vec2( gl_LaunchIDEXT.xy ) + vec2( rnd( seed ), rnd( seed ) );
    const vec2 normalizedPixel = positionWithinPixel / vec2( gl_LaunchSizeEXT.xy );
    vec2 d                     = normalizedPixel * 2.0 - 1.0;

    // depth of field
    float aperture      = cam.position.w;
    float focusDistance = cam.viewingDirection.w;
    vec2 offset         = aperture / 2.0 * diskSampling( ray.seed );

    vec4 origin, direction;
    vec4 target = cam.projInverse * vec4( d.x, d.y, 1.0, 1.0 );

    if ( aperture > 0.0 )
    {
      origin    = cam.viewInverse * vec4( offset, 0.0, 1.0 );
      direction = cam.viewInverse * vec4( normalize( target.xyz * focusDistance - vec3( offset, 0.0 ) ), 0.0 );
    }
    else
    {
      origin    = cam.viewInverse * vec4( 0.0, 0.0, 0.0, 1.0 );
      direction = cam.viewInverse * vec4( normalize( target.xyz ), 0.0 );
    }

    ray.direction  = direction.xyz;
    ray.origin     = origin.xyz;
    ray.weight     = vec3( 0.0 );
    ray.emission   = vec3( 1.0 );
    ray.reflective = false;
    ray.refractive = false;

    vec3 weight = vec3( 1.0 );
    vec3 color  = vec3( 0.0 );

    uint rayFlags = gl_RayFlagsNoneEXT;
    float tMin    = 0.001;
    float tMax    = 10000.0;

    int refractionCounter = 0;

    for ( ray.depth = 0; ray.depth <= maxPathDepth; ++ray.depth )
    {
      // refractive rays are allowed to bounce more time to have the all effects at lower sample rates
      if ( ray.refractive )
      {
        ray.depth -= 1;
        ++refractionCounter;
      }

      // but to make sure this is not an endless loop, quit after five refraction bounces
      if ( refractionCounter > 5 )
      {
        ray.depth = maxPathDepth + 1;
      }

      traceRayEXT( topLevelAS,    // acceleration structure
                   rayFlags,      // rayFlags
                   0xFF,          // cullMask
                   0,             // sbtRecordOffset
                   0,             // sbtRecordStride
                   0,             // missIndex
                   ray.origin,    // ray origin
                   tMin,          // ray min range
                   ray.direction, // ray direction
                   tMax,          // ray max range
                   0 );           // payload (location = 0)

      color += ray.emission * weight;
      weight *= ray.weight;

      // Russian roulette
      // Randomly terminate a path with a probability inversely equal to the throughput
      if ( russianRoulette && ray.depth >= russianRouletteMinBounces )
      {
        float p = max( weight.x, max( weight.y, weight.z ) );
        //float rand = clamp( rnd( ray.seed ), 0.0, 1.0 );
        float rand = rnd( ray.seed );

        if ( rand > p )
        {
          //ray.depth = maxPathDepth + 1;
          break;
        }

        // Add the energy "lost" by randomly terminating paths (this only happens if a ray was not terminated)
        weight *= 1.0 / p;
      }
    }

#ifdef VARIANCE
    meanX += color.x;
    valuesX[sampleRatePerPixel] = color.x;

    meanY += color.y;
    valuesY[sampleRatePerPixel] = color.y;

    meanZ += color.z;
    valuesZ[sampleRatePerPixel] = color.z;
#endif

    colors += color;
  }

  // weighted average
  vec3 finalColor = colors / sampleRatePerPixel;

#ifdef HEATMAP
  uint time          = uint( clockARB( ) ) - timeStart;
  finalColor         = vec3( 0.0 );
  float heatmapScale = 1000000.0 * 1.5 * 1.5;
  finalColor         = heatmap( clamp( float( time ) / heatmapScale, 0.0, 1.0 ) );
#endif

#ifdef VARIANCE
  // write sample to variance buffer
  meanX /= sampleRatePerPixel;
  meanY /= sampleRatePerPixel;
  meanZ /= sampleRatePerPixel;

  float sumX = 0.0;
  float sumY = 0.0;
  float sumZ = 0.0;

  for ( uint i = 0; i < sampleRatePerPixel; ++i )
  {
    float deviationX = valuesX[i] - meanX;
    sumX += deviationX * deviationX;

    float deviationY = valuesY[i] - meanY;
    sumY += deviationY * deviationY;

    float deviationZ = valuesZ[i] - meanZ;
    sumZ += deviationZ * deviationZ;
  }

  float div  = sampleRatePerPixel == 1 ? 1.0 : ( sampleRatePerPixel - 1 ); // is this even valid?
  float varX = sumX / div;
  float varY = sumY / div;
  float varZ = sumZ / div;

  varianceBuffer.v[mapping] = ( varX + varY + varZ ) / 3.0;
#endif

  // tone mapping
  //const float limit = 1.5;
  //float luminance   = 0.3 * finalColor.x + 0.6 * finalColor.y + 0.1 * finalColor.z;
  //finalColor *= ( 1.0 / ( 1.0 + luminance / limit ) );

  // Interpolate results over time (length defined on host as frame count (int))
  // First frame
  if ( frameCount <= 0 )
  {
    imageStore( image, ivec2( gl_LaunchIDEXT.xy ), vec4( finalColor, 1.0 ) );
  }
  // Following frames: linearly interpolate between previous and current color.
  else
  {
    vec3 oldColor = imageLoad( image, ivec2( gl_LaunchIDEXT.xy ) ).xyz;
    vec4 temp     = vec4( mix( oldColor, finalColor, 1.0 / float( frameCount + 1 ) ), 1.0 );
    imageStore( image, ivec2( gl_LaunchIDEXT.xy ), temp );
  }
}
